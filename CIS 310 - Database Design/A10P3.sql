USE [CIS31026]
IF SCHEMA_ID('DW') IS NULL EXEC('CREATE SCHEMA DW')
ALTER USER [CIS31026] WITH DEFAULT_SCHEMA = [DW]
IF OBJECT_ID('DW.PopulateAirDW', 'P') IS NOT NULL DROP PROCEDURE [DW].[PopulateAirDW]
GO
CREATE PROCEDURE [DW].[PopulateAirDW]
AS
BEGIN
	IF OBJECT_ID('FK_FACT_PILOT', 'F') IS NOT NULL 
		ALTER TABLE [DW].[Fact]
			DROP CONSTRAINT FK_FACT_PILOT;
	IF OBJECT_ID('FK_FACT_MODEL', 'F') IS NOT NULL 
		ALTER TABLE [DW].[Fact]
			DROP CONSTRAINT FK_FACT_MODEL;
	IF OBJECT_ID('FK_FACT_TIME', 'F') IS NOT NULL 
		ALTER TABLE [DW].[Fact]
			DROP CONSTRAINT FK_FACT_TIME;
	
	TRUNCATE TABLE [DW].[Fact];
	TRUNCATE TABLE [DW].[PilotDimension];
	TRUNCATE TABLE [DW].[ModelDimension];
	TRUNCATE TABLE [DW].[TimeDimension];
	TRUNCATE TABLE [DW].[STAGING];

	ALTER TABLE [DW].[Fact] ADD
		CONSTRAINT FK_FACT_PILOT FOREIGN KEY (PilotID) REFERENCES [DW].[PilotDimension](PilotID),
		CONSTRAINT FK_FACT_MODEL FOREIGN KEY (ModelID) REFERENCES [DW].[ModelDimension](ModelID),
		CONSTRAINT FK_FACT_TIME FOREIGN KEY (TimeID) REFERENCES [DW].[TimeDimension](TimeID);	

	INSERT INTO [DW].[PilotDimension]
		SELECT E.EMP_NUM, E.EMP_LNAME, E.EMP_FNAME, P.PIL_LICENSE, P.PIL_RATINGS, P.PIL_MED_TYPE, P.PIL_MED_DATE, P.PIL_PT135_DATE 
		FROM [AIR].[EMPLOYEE] E INNER JOIN [AIR].[PILOT] P ON E.EMP_NUM = P.EMP_NUM;
	
	INSERT INTO [DW].[ModelDimension]
		SELECT M.MOD_CODE, M.MOD_MANUFACTURER, M.MOD_NAME, M.MOD_SEATS, M.MOD_CHG_MILE
		FROM [AIR].[MODEL] M;

	INSERT INTO [DW].[TimeDimension]
		SELECT DISTINCT CHAR_DATE, YEAR(CHAR_DATE), MONTH(CHAR_DATE)
		FROM [AIR].[CHARTER];


	INSERT INTO [DW].[STAGING] ([EMP_NUM], [MOD_CODE], [CHAR_DATE], [CHAR_DISTANCE], [CHAR_FUEL_GALLONS], [REVENUE])
		SELECT P.EMP_NUM, M.MOD_CODE, C.CHAR_DATE, SUM(C.CHAR_DISTANCE) AS [DISTANCE], SUM(C.CHAR_FUEL_GALLONS) AS [FUEL], SUM(M.MOD_CHG_MILE*C.CHAR_FUEL_GALLONS) AS REVENUE
		FROM [AIR].[CHARTER] C INNER JOIN [AIR].[CREW] CR ON C.CHAR_TRIP = CR.CHAR_TRIP
					INNER JOIN [AIR].[PILOT] P ON CR.EMP_NUM = P.EMP_NUM
					INNER JOIN [AIR].[AIRCRAFT] A ON C.AC_NUMBER = A.AC_NUMBER
					INNER JOIN [AIR].[MODEL] M ON A.MOD_CODE = M.MOD_CODE
		GROUP BY P.EMP_NUM, M.MOD_CODE, C.CHAR_DATE;

		UPDATE STAGING
		SET [PilotID] = P.[PilotID], [ModelID] = M.[ModelID], [TimeID] = T.[TimeID]
		FROM [DW].[STAGING] S INNER JOIN [DW].[PilotDimension] P ON S.EMP_NUM = P.EMP_NUM
			INNER JOIN [DW].[ModelDimension] M ON S.MOD_CODE = M.MOD_CODE
			INNER JOIN [DW].[TimeDimension] T ON S.CHAR_DATE = T.CHAR_DATE

	INSERT INTO [DW].[Fact] ([PilotID], [ModelID], [TimeID], [CHAR_FUEL_GALLONS], [CHAR_DISTANCE], [REVENUE])
		SELECT [PilotID], [ModelID], [TimeID], [CHAR_FUEL_GALLONS], [CHAR_DISTANCE], [REVENUE] 
		FROM [DW].[STAGING];
END;
GO

[DW].[PopulateAirDW];